apply plugin: 'com.android.application'

android {
    compileOptions.encoding = "UTF-8"
    version = project.APP_VERSION
    project.ext.set("defaultDeployPhase", "${project.hasProperty('deploy_phase') ? deploy_phase.toString() : "$DEFAULT_PHASE"}")

    compileSdkVersion ANDROID_BUILD_SDK_VERSION
    buildToolsVersion ANDROID_BUILD_TOOL_VERSION

    defaultConfig {
        minSdkVersion ANDROID_BUILD_MIN_SDK_VERSION
        targetSdkVersion ANDROID_BUILD_TARGET_SDK_VERSION
        versionCode Integer.parseInt(project.APP_VERSION)
        versionName project.APP_VERSION_NAME
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            jniDebuggable false
            zipAlignEnabled true
        }
        debug {
            debuggable true
            minifyEnabled false
            zipAlignEnabled true
        }
    }

    // sample app에선 java 1.6 기반으로 작성되었다.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
        checkAllWarnings true
        xmlReport true
        htmlReport true
        disable "InvalidPackage", "MissingTranslation"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            println output
            output.processManifest << {
                println ">> processManifest ($output.processManifest.manifestOutputFile)"
                def appKeyName = 'kakao_app_key'
                def appSchemeName = 'kakao_scheme'
                def file = output.processManifest.manifestOutputFile
                replaceString(file, appKeyName, addPrefix(appKeyName))
                replaceString(file, appSchemeName, addPrefix(appSchemeName))
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // 카카오스토리, 카카오톡 sdk를 사용하기 위해 필요.
    compile 'com.mcxiaoke.volley:library:1.0.8'
    compile group: 'com.android.support', name: 'support-v4', version: project.SUPPORTV4_VERSION

    // 카카오스토리 sdk를 사용하기 위해 필요.
    compile group: project.KAKAO_SDK_GROUP, name: getModuleName('kakaostory'), version: project.KAKAO_SDK_VERSION

    // 카카오톡 sdk를 사용하기 위해 필요.
    compile group: project.KAKAO_SDK_GROUP, name: getModuleName('kakaotalk'), version: project.KAKAO_SDK_VERSION

    // push sdk를 사용하기 위해 필요.
    compile group: project.KAKAO_SDK_GROUP, name: getModuleName('push'), version: project.KAKAO_SDK_VERSION

    // storage sdk를 사용하기 위해 필요.
    compile group: project.KAKAO_SDK_GROUP, name: getModuleName('storage'), version: project.KAKAO_SDK_VERSION
}

def addPrefix(value) {
    def result = value
    if(project.defaultDeployPhase.toLowerCase() != 'release') {
        result = project.defaultDeployPhase.toLowerCase() + "_" + result
    }

    println result
    return result
}

def replaceString(file, fromString, toString) {
    def updatedContent = file.getText('UTF-8').replaceAll(fromString, toString)
    file.write(updatedContent, 'UTF-8')
}

def getModuleName(moduleName) {
    println moduleName + ", " + project.defaultDeployPhase
    def result = moduleName
    if(project.defaultDeployPhase.toLowerCase() == 'release') {
        return result
    }

    return result + "-${project.defaultDeployPhase.toLowerCase()}"
}
